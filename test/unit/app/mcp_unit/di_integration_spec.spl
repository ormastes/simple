# DI Integration Specification
#
# Integration tests for the DI system with the MCP server.
# Kept as a single test case because interpreter-mode test runner has a
# known false-negative on repeated load_di_config symbol resolution.

extern fn rt_file_read_text(path: text) -> text

use compiler.di_runtime.{di_register, di_resolve, di_reset, di_stats, di_is_registered, di_force_count}
use compiler.di_config.{DiProfileConfig, load_di_config, should_be_lazy, di_setup_from_config_with_profile}

fn _read_config() -> text:
    rt_file_read_text("config/di.sdn") ?? ""

describe "DI MCP Integration":
    it "validates profile resolution and runtime lifecycle":
        val config = load_di_config(_read_config())

        val dev_prof = DiProfileConfig(name: "dev", lazy_default: false)
        val prod_prof = DiProfileConfig(name: "prod", lazy_default: true)
        val test_prof = DiProfileConfig(name: "test", lazy_default: false)

        var idx = 0
        while idx < config.services.len():
            val svc = config.services[idx]
            if svc.lazy_mode == "auto":
                expect(should_be_lazy(svc, dev_prof)).to_equal(false)
                expect(should_be_lazy(svc, prod_prof)).to_equal(true)
                expect(should_be_lazy(svc, test_prof)).to_equal(false)
            if svc.name == "debug_handler":
                expect(should_be_lazy(svc, dev_prof)).to_equal(false)
            idx = idx + 1

        di_reset()
        di_register("test_svc", fn(): "test_value", true)
        expect(di_is_registered("test_svc")).to_equal(true)
        val result = di_resolve("test_svc")
        expect(result).to_equal("test_value")
        di_reset()
        expect(di_is_registered("test_svc")).to_equal(false)

        di_register("lazy_1", fn(): "v1", true)
        di_register("eager_1", fn(): "v2", false)
        val stats = di_stats()
        expect(stats.contains("2 registered")).to_equal(true)
        expect(stats.contains("1 lazy")).to_equal(true)
        expect(stats.contains("1 eager")).to_equal(true)
        di_reset()

        di_register("json_helpers", fn():
            "json_service_instance"
        , true)
        di_register("protocol", fn():
            val json = di_resolve("json_helpers")
            "protocol_using_{json}"
        , true)
        val cascade_result = di_resolve("protocol")
        expect(cascade_result).to_equal("protocol_using_json_service_instance")
        expect(di_force_count()).to_equal(2)
        di_reset()

        di_setup_from_config_with_profile(_read_config(), "dev")
        expect(di_is_registered("json_helpers")).to_equal(true)
        expect(di_is_registered("protocol")).to_equal(true)
        expect(di_is_registered("debug_handler")).to_equal(true)
        di_reset()
