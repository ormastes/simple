#!/usr/bin/env simple
# Duplication detection CLI

use app.duplicate_check.config (load_config, default_config, DuplicationConfig)
use app.duplicate_check.detector (find_duplicates, collect_files)
use app.duplicate_check.formatter (print_text_report, print_json_report, write_sdn_database)
use app.duplicate_check.debug (init_debug)
use app.duplicate_check.cache.{new_token_cache_manager, get_cache_stats}
use app.io.mod.{file_read, file_write, cli_get_args}

fn parse_args(args: [text]) -> DuplicationConfig:
    var config = load_config()
    var target_path = "src/"

    var i = 0
    while i < args.len():
        val arg = args[i]

        if arg == "--min-tokens" and i + 1 < args.len():
            config.min_tokens = int(args[i + 1])
            i = i + 2
        elif arg == "--min-lines" and i + 1 < args.len():
            config.min_lines = int(args[i + 1])
            i = i + 2
        elif arg == "--min-impact" and i + 1 < args.len():
            config.min_impact = int(args[i + 1])
            i = i + 2
        elif arg == "--format" and i + 1 < args.len():
            config.output_format = args[i + 1]
            i = i + 2
        elif arg == "--exclude" and i + 1 < args.len():
            config.exclude_patterns = config.exclude_patterns + [args[i + 1]]
            i = i + 2
        elif arg == "--help" or arg == "-h":
            print "Usage: simple duplicate-check [path] [options]"
            print ""
            print "Options:"
            print "  --min-tokens N      Minimum tokens per duplicate (default: 30)"
            print "  --min-lines N       Minimum lines per duplicate (default: 5)"
            print "  --min-impact N      Minimum impact score to report (default: 50)"
            print "  --format FORMAT     Output format: text, json, sdn (default: text)"
            print "  --exclude PATTERN   Exclude files matching pattern"
            print "  --help, -h          Show this help"
            exit(0)
        elif not arg.starts_with("-"):
            target_path = arg
            i = i + 1
        else:
            i = i + 1

    config

fn main():
    init_debug()

    val args = cli_get_args()

    var cmd_args = []
    var i = 2
    while i < args.len():
        cmd_args = cmd_args + [args[i]]
        i = i + 1

    val config = parse_args(cmd_args)

    val target_path = if cmd_args.len() > 0 and not cmd_args[0].starts_with("-"):
        cmd_args[0]
    else:
        "src/"

    val files = collect_files(target_path, config)

    if files.len() == 0:
        print "No files found to scan"
        exit(1)

    val cache_manager = new_token_cache_manager()

    val groups = find_duplicates(files, config, cache_manager)

    print ""
    print get_cache_stats(cache_manager)
    print ""

    if config.output_format == "json":
        print_json_report(groups)
    elif config.output_format == "sdn":
        write_sdn_database(groups, config.report_path)
        print_text_report(groups)
    else:
        print_text_report(groups)

    val exit_code = if groups.len() > config.max_allowed: 1 else: 0
    exit(exit_code)
